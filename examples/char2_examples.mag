/************************************
 *  Created on 4/29/2022 by PAB.    *
 *  Examples and constructions of   * 
 *  *-algebras in characteristic 2. *
*************************************/

/*
   Construct a "random" *-subalgebra R 
   of M_d(GF(p^e)) such that both 
   J = J(R) and J^2 are nonzero.

   "random" is a bit of an exaggeration 
   here: to make sure we get something 
   substantial it just constructs the 
   adjoint algebra of a pair of 
   symplectic groups.

   The "INTERESTING" flag, if true, also
   requires that the constructed 
   Wedderburn complement to the radical 
   if NOT *-invariant.
*/
RandomStarAlgebraWithRadical := function (d, p, e : INTERESTING := false)
  q := p^e;
  G1 := Sp (d, q);
  F1 := ClassicalForms (G1)`bilinearForm;
  found := false;
  count := 0;
  LIMIT := 1000;
  while (count lt LIMIT) and (not found) do
    count +:= 1;
    G2 := RandomConjugate (G1);
    F2 := ClassicalForms (G2)`bilinearForm;
    R := AdjointAlgebraChar2 ([F1, F2]);
    J, S := WedderburnDecomposition (R);
    if (Dimension (J) gt 0) and (Dimension (J*J) gt 0) then  
         if INTERESTING then 
              if not IsStarSubalgebra (R, S) then 
                   found := true;
              end if;
         else   
              found := true;
         end if;
    end if;
  end while;
  if found then 
    return R;
  else
    "failed to find an example after", LIMIT, "tries";
    return false;
  end if;
end function;


/*
  This is the basic example from the paper 
  of a *-algebra R in which not all 
  unitarians of R/J(R) lift to unitarians 
  of R. It is also a "generic" example of 
  this type in that the inner derivation 
  corresponding to the natural radical-semisimple 
  decomposition of R is a (non-zero) element 
  of the key space W = (ker tr) / (im tr) 

  The input is just a positive integer e 
  to build a "basic generic" example over 
  the field GF(2^e).
*/

/*

*** PAB: this was my original way to build it 

     __BGE_star_image := function (M, t, one)
          N := M;
          InsertBlock (~N, (one + t) * ExtractBlock (M, 1, 3, 2, 2), 3, 1);
          InsertBlock (~N, (one + t) * ExtractBlock (M, 3, 1, 2, 2), 1, 3);
     return N;
     end function;

BasicGenericExample := function (e)
  K := GF (2^e);
  ma := MatrixAlgebra (K, 2);
    // make truncated poly algebra as algebra of 2 x 2 matrices
  t := ma![0,1,0,0];
  one := Identity (ma);
    // build 2 x 2 matrix algebra over this ring
  MA := MatrixAlgebra (K, 4);
  z := MA!0;
  R := sub < MA | 
             [
             InsertBlock (z, t, 1, 1),
             InsertBlock (z, one, 1, 1),
             InsertBlock (z, t, 1, 3),
             InsertBlock (z, one, 1, 3),
             InsertBlock (z, t, 3, 1),
             InsertBlock (z, one, 3, 1),
             InsertBlock (z, t, 3, 3),
             InsertBlock (z, one, 3, 3)
             ]
           >;
  star := hom < R -> R | M :-> __BGE_star_image (M, t, one) >;
  R`Star := star;
return R;
end function;
*/

// PAB: this is a new way that extends more readily to higher nilpotence
     __BGE_star_image := function (M)
          N := 0 * M;
          A := ExtractBlock (M, 1, 1, 2, 2);
          AJ := 0 * A; AJ[1][2] := A[2][1]; AJ[2][1] := A[1][2];
          B := ExtractBlock (M, 1, 3, 2, 2);
          InsertBlock (~N, Transpose (A), 1, 1);
          InsertBlock (~N, Transpose (A), 3, 3);
          InsertBlock (~N, AJ + Transpose (B), 1, 3);
     return N;
     end function;


BasicGenericExample := function (e)
  K := GF (2^e);
  ma := MatrixAlgebra (K, 2);
  MA := MatrixAlgebra (K, 4);
  Z := MA!0;
  R_gens := [ DiagonalJoin (ma.i, ma.i) : i in [1..Ngens (ma)] ];
  R_gens cat:= [ InsertBlock (Z, ma.i, 1, 3) : i in [1..Ngens (ma)] ];
  R := sub < MA | R_gens >;
  R`Star := hom < R -> R | M :-> __BGE_star_image (M) >;
return R;
end function;


// new example by MK to illustrate more complex generic behavior

     __EGE_star_image := function (M)
          N := 0 * M;
          A := ExtractBlock (M, 1, 1, 2, 2);
          AJ := 0 * A; AJ[1][2] := A[2][1]; AJ[2][1] := A[1][2];
          B := ExtractBlock (M, 1, 4, 2, 2);
          InsertBlock (~N, Transpose (A), 1, 1);
          InsertBlock (~N, Transpose (A), 4, 4);
          InsertBlock (~N, AJ + Transpose (B), 1, 4);
          N[3][3] := M[3][3];
          v := ExtractBlock (M, 1, 3, 2, 1);
          w := ExtractBlock (M, 3, 4, 1, 2);
          InsertBlock (~N, Transpose (w), 1, 3);
          InsertBlock (~N, Transpose (v), 3, 4);
     return N;
     end function;

ExtendedGenericExample := function (e)
  K := GF (2^e);
  ma := MatrixAlgebra (K, 2);
  MA := MatrixAlgebra (K, 5);
  Z := MA!0;
  R_gens := [ DiagonalJoin (DiagonalJoin (ma.i, Matrix ([[K!0]])), ma.i) : i in [1..Ngens (ma)] ];
  K_gen := Z; K_gen[3][3] := K.1; Append (~R_gens, K_gen);
  V_gen := Z; V_gen[1][3] := 1; Append (~R_gens, V_gen);
  W_gen := Z; W_gen[3][4] := 1; Append (~R_gens, W_gen);
  R_gens cat:= [ InsertBlock (Z, ma.i, 1, 3) : i in [1..Ngens (ma)] ];
  R := sub < MA | R_gens >;
  R`Star := hom < R -> R | M :-> __EGE_star_image (M) >;
return R;
end function;

/*
  Here is the quadratic form example from the paper.
  It can use any quadratic form, but this basic 
  analogue of the generic example above uses the 
  standard quadratic form for the data provided
*/

BasicExoticExample := function (d, e : Minus := false)
  PHI := StandardQuadraticForm (d, GF(2^e) : Minus := Minus);
return QuadraticFormToStarAlgebra (PHI);
end function;

// at the moment, need to load these "My__" functions
test_random := function (d, e : INTERESTING := false)
  R := RandomStarAlgebraWithRadical (d, 2, e : INTERESTING := INTERESTING);
  D := R;
  J, S := MyWedderburnDecomposition (D);
  "J has dimension", Dimension (J);
  "J^2 has dimension", Dimension (J*J);
  "J^3 has dimension", Dimension (J*J*J);
  "S is *-invariant?", IsStarSubalgebra (R, S);
  Q := J;
  w, gen := MyDerivation (D, J, Q, S);
return gen;
end function;

